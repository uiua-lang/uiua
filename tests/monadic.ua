# Range
⍤⤙≍ [0 1 2 3 4 5] ⇡6
⍤⤙≍ [¯1 ¯2 ¯3 ¯4 ¯5 ¯6] ⇡¯6
⍤⤙≍ [[0_0 0_1 0_2] [1_0 1_1 1_2] [2_0 2_1 2_2]] ⇡3_3
⍤⤙≍ [[0_0 0_1 0_2] [1_0 1_1 1_2]] ⇡2_3
⍤⤙≍ [[¯1_0 ¯1_1 ¯1_2] [¯2_0 ¯2_1 ¯2_2]] ⇡¯2_3

# Deshape
⍤⤙≍ [5] ♭5
⍤⤙≍ [5] ♭[5]
⍤⤙≍ [5] ♭[[5]]
⍤⤙≍ [1 2 3] ♭ [1 2 3]
⍤⤙≍ [1 2 3 4] ♭ [1_2 3_4]

# Subscripted deshape
⍤⤙≍ [] △ ♭₀ ↯2_3_4_5⇡120
⍤⤙≍ [120] △ ♭₁ ↯2_3_4_5⇡120
⍤⤙≍ [24 5] △ ♭₂ ↯2_3_4_5⇡120
⍤⤙≍ [6 4 5] △ ♭₃ ↯2_3_4_5⇡120
⍤⤙≍ [2 3 4 5] △ ♭₄ ↯2_3_4_5⇡120
⍤⤙≍ [1 2 3 4 5] △ ♭₅ ↯2_3_4_5⇡120
⍤⤙≍ [1 1 2 3 4 5] △ ♭₆ ↯2_3_4_5⇡120
⍤⤙≍ [6 4 5] △ ♭₋₁ ↯2_3_4_5⇡120
⍤⤙≍ [24 5] △ ♭₋₂ ↯2_3_4_5⇡120
⍤⤙≍ [120] △ ♭₋₃ ↯2_3_4_5⇡120

# Reverse
⍤⤙≍ 5 ⇌5
⍤⤙≍ [3 2 1] ⇌[1 2 3]
⍤⤙≍ [5_6 3_4 1_2] ⇌[1_2 3_4 5_6]

# Transpose
⍤⤙≍ 5 ⍉5
⍤⤙≍ [1 2 3] ⍉[1 2 3]
⍤⤙≍ [1_4 2_5 3_6] ⍉[1_2_3 4_5_6]
⍤⤙≍ ⍉⍉.↯3_4⇡12
⍤⤙≍ °⍉⍉.↯3_4⇡12
⍤⤙≍ [[0_1 0_0] [0_1 1_1] [0_1 2_2]] ⍉⇡2_3
⍤⤙≍ [[0_0_0 1_1_1] [0_1_2 0_1_2]] ⍉⍉⇡2_3
⍤⤙≍ [[0_0_0 1_1_1] [0_1_2 0_1_2]] °⍉⇡2_3
⍤⤙≍ ⊃(⍉⍉)°⍉ ↯2_3_4⇡24
⍤⤙≍ ⊃°(⍉⍉)⍉ ↯2_3_4⇡24
⍤⤙≍ ⊃(⍉⍉)°(⍉⍉) ↯2_3_4_5⇡120
⍤⤙≍ ⊃°(⍉⍉)(⍉⍉) ↯2_3_4_5⇡120
⍤⤙≍ ⊃⍉°(⍉⍉⍉) ↯2_3_4_5⇡120
⍤⤙≍ ⊃°⍉(⍉⍉⍉) ↯2_3_4_5⇡120
⍤⤙≍ ⍉⍉⍉⍉ . ↯2_3_4_5⇡120
⍤⤙≍ □[1_3 2_4] ⍉□[1_2 3_4]
⍤⤙≍ {[1_3 2_4] [5_7 6_8]} ≡⍉{[1_2 3_4] [5_6 7_8]}
⍤⤙≍ ⊃⍜⍉⇌⍜(⍉⍉⍉)⇌ [[1 2 3] [4 5 6] [7 8 9]]

# Subscripted length
⍤⤙≍ 3 ⧻₁ °△2_3_4_5
⍤⤙≍ 4 ⧻₋₂ °△2_3_4_5

# Subscripted shape
⍤⤙≍ 2_3 △₂ °△2_3_4_5
⍤⤙≍ 4_5 △₋₂ °△2_3_4_5

# Parse
⍤⤙≍ 5 ⍣⋕0 "5"
⍤⤙≍ 0 ⍣⋕0 "dog"
⍤⟜≍ ⟜∵⍜°⋕∘ [0 ¯1 η π τ ∞ ¯π NaN]
⍤⤙≍ [12 34 56] ⋕ ["12""34""56"]
⍤⤙≍ [1 23 456] ⋕ {"1""23""456"}
⍤⤙≍ "5" °⋕5
⍤⤙≍ {"1""2""3"} °⋕[1 2 3]
⍤⤙≍ [2 10 5] ⬚5⋕{"2" "10" "dog"}
⍤⤙≍ [ℂ 3.4e6 1.2 ℂ π ∞] ⋕ {"1.2r3.4e6i" "infrpii"}
⍤⟜≍ ⋕°⋕. [ℂ 3.4e6 1.2 ℂ π ∞]
⍤⤙≍ [ℂ4.5 ¯12.3 ℂ4.5 12.3 ℂ¯4.5 12.3 ℂ¯4.5 12.3 ℂ4.5 12.3] ⋕{"-12.3r4.5i" "12.3r4.5i" "12.3-4.5i" "12.3r-4.5i" "12.3r4.5i"}
⍤⤙≍ [ℂ4 ¯12 ℂ4 12 ℂ¯4 12 ℂ¯4 12 ℂ4 12] ⋕{"-12r4i" "12r4i" "12-4i" "12r-4i" "12r4i"}
⍤⤙≍ [ℂ¯∞ π ℂ∞ π ℂτ π ℂ¯τ π ℂτ ¯π] ⋕{"πr-∞i" "πr∞i" "πrτi" "πr-τi" "-πrτi"}
⍤⤙≍ [ℂ¯∞ π ℂ∞ π ℂτ π ℂ¯τ π ℂτ ¯π] ⋕{"π-∞i" "π+∞i" "π+τi" "π-τi" "-π+τi"}
⍤⟜≍: ["12.1 " " 3   " " 6.25"] ⬚@ °⋕ [12.1 3 6.25]
⍤⟜≍: ["12.10" "03.00" "06.25"] ⬚@0°⋕ [12.1 3 6.25]
⍤⤙≍ ["001""382""023"] ⬚@0°⋕ [1 382 23]
⍤⤙≍ ¯1 ⬚¯1⋕ "Franklin County, Mississippi"
⍤⤙≍ map "ab" [123 456] ⋕ map "ab" {"123" "456"}
⍤⤙≍ map "ab" {"123" "456"} °⋕ map "ab" [123 456]

# Where
⍤⤙≍ [0 3 5 6] ⊚[1 0 0 1 0 1 1 0]
⍤⤙≍ [1 2 2 3 3 3 4 4 4 4] ⊚[0 1 2 3 4]
⍤⤙≍ [1 0 0 1 0 1 1] °⊚[0 3 5 6]
⍤⤙≍ [1 0 0 1 0 1 1] °⊚[6 0 3 5]
⍤⤙≍ [0 1 2 3 4] °⊚[1 2 2 3 3 3 4 4 4 4]
# Multi-dimensional where
⍤⤙≍ [0_2 1_1 1_2 1_2] ⊚[0_0_1 0_1_2 0_0_0]
⍤⤙≍ [0_0_0_0 0_2_0_0 0_0_0_1] °⊚[2_3 1_1 1_1]

# Classify
⍤⤙≍ [0 1 2 3 4] ⊛ [2 9 4 8 3]
⍤⤙≍ [0 1 2 3 4] ⊛ ↯5_2⇡10
⍤⤙≍ [0 1 0 1 0] ⊛ ↯5_2⇡4

A ← [[1_2 1_2] [3_3 1_2]]
⍤⤙≍ [0 1] ⊛ A
⍤⤙≍ [0_0 0_1] ≡⊛ A
⍤⤙≍ [[0_1 0_1] [0_0 0_1]] ≡≡⊛ A
⍤⤙≍ [[0_0 0_0] [0_0 0_0]] ≡≡≡⊛ A

# Deduplicate
⍤⤙≍ [1 2 3 4 5] ◴ [1 2 3 4 5]
⍤⤙≍ [1 2 3 4 5] ◴ [1 2 3 4 5 5 5 5 5 5]
⍤⤙≍ [1 2 3 4 5] ◴ [1 2 2 2 2 2 3 4 5 5]
⍤⤙≍ [4 8 2 9 1 3] ◴ [4 8 2 9 1 8 3 9 4 9 2 8]
⍤⤙≍ [1_2 3_4 5_6] ◴ [1_2 3_4 5_6 3_4]

# Unique
⍤⤙≍ [1 1 1 1 1] ◰ [1 2 3 4 5]
⍤⤙≍ [1 1 1 1 1 0 0 0 0 0] ◰ [1 2 3 4 5 5 5 5 5 5]
⍤⤙≍ [1 1 1 1 1 0 1 0 0 0 0 0] ◰ [4 8 2 9 1 8 3 9 4 9 2 8]
⍤⤙≍ [1 1 1 0] ◰ [1_2 3_4 5_6 3_4]

# Bits
⍤⟜≍ ⟜⍜⋯∘ 5
⍤⟜≍ ⟜⍜⋯∘ 15
⍤⟜≍ ⟜⍜⋯∘ [1 2 3]
⍤⟜≍ ⟜⍜⋯∘ [1_2_3 4_5_6]
⍤⟜≍ ⟜⍜°⋯∘ [1 0 1]
⍤⟜≍ ⟜⍜°⋯∘ [1 1 1 1]
⍤⟜≍ ⟜⍜°⋯∘ [0_1 1_0]
⍤⟜≍ ⟜⍜⋯∘ 0
⍤⟜≍ ⟜⍜⋯∘ [0]
⍤⟜≍ ⟜⍜⋯∘ [0 0 0]
⍤⟜≍ ⟜⍜⋯∘ [[]]
⍤⟜≍ ⟜⍜⋯∘ [[0]]
⍤⟜≍ ⟜⍜°⋯∘ []
⍤⟜≍ ⟜⍜°⋯∘ [[]]

⍤⤙≍ 13 °⋯₃ [1 0 1 1]
⍤⤙≍ [1 0 1 0 0 0 0 0] ⋯₈ 5

# Json
⍤⤙≍ "[1,2,3]" json [1 2 3]
⍤⤙≍ "[\"some\",\"words\"]" json {"some" "words"}
⍤⤙≍ "{\"buddy\":[3,4,5],\"hey\":1,\"there\":2}" json map {"hey" "there" "buddy"} {1 2 [3 4 5]}
⍤⤙≍ "[true,false]" json [True False]
⍤⤙≍ [4 5 6] °json "[4,5,6]"
⍤⤙≍ {"what's" "the" "plan"} °json $ ["what's","the","plan"]
⍤⤙≍ ⇌map {"foo" "baz"} {"bar" 1_2_3} °json $ {"foo": "bar", "baz": [1, 2, 3]}
⍤⤙≍ [True False] °json "[true,false]"
json insert "b" True map {"a"} {1_2_3_4}
⍤⤙≍ $ {"a":[1,2,3,4],"b":true}

# Csv
⍤⟜≍ ⟜⍜csv∘ [{"hi" "there"} {"my" "friend"}]
⍤⤙≍ ⋕ ⍜csv∘ . ↯3_4⇡12
⍤⤙≍ ∵⍣⋕∘ ⍜csv∘ . [{"Foo" "Bar"} [1 2] [3 4] [5 6]]
⍤⤙≍ [{"1" "2" ""} {"3" "" ""} {"4" "5" "6"}] °csv "1,2\n3\n4,5,6\n"
⍤⤙≍ [{"1" "2" ""} {"3" "" ""} {"4" "5" "6"}] °⬚@;csv "1;2\n3\n4;5;6\n"
⍤⤙≍ [{"1" "2" "x"} {"3" "x" "x"} {"4" "5" "6"}] ⬚"x"°⬚@;csv "1;2\n3\n4;5;6\n"
⍤⤙≍ "1,2\n3\n4,5,6\n" csv {1_2 3 4_5_6}
⍤⤙≍ "1;2\n3\n4;5;6\n" ⬚@;csv {1_2 3 4_5_6}

# Datetime
⍤⤙≍ [2023 2 28 1 2 3] ⍜°datetime∘ [2023 2 28 1 2 3]
⍤⤙≍ [2023 3 1 1 2 3] ⍜°datetime∘ [2023 2 29 1 2 3]
⍤⤙≍ [2024 1 1 0 0 0] ⍜°datetime∘ [2024]
⍤⤙≍ [2024 7 1 0 0 0] ⍜°datetime∘ [2024.5]
⍤⤙≍ [2023 12 1 0 0 0] ⍜°datetime∘ [2024 0]
⍤⤙≍ [2023 11 30 0 0 0] ⍜°datetime∘ [2024 0 0]

# Repr
repr[{[[1 2][2 3]][3 4]}{[[1 2][2 3]]□{[[1 2][2 3]]□□[3 4]}}]
"[{[[1 2] [2 3]] [3 4]}\n  {[[1 2] [2 3]] □{[[1 2] [2 3]] □□[3 4]}}]"
⍤⤙≍
⍤⤙≍ {"\"test\"" "ℂ1.5 ¯0.0004" "□\"box\""} ⍚repr {"test" ℂ3/2 ¯4e¯4 □"box"}
⍚repr{@⊗ @  @\0 @' @\\ @\u{1f600} @\x13 @\u{99999} "你好"}
⍤⤙≍ {"@⊗" "@\\s" "@\\0" "@'" "@\\\\" "@😀" "@\\x13" "@\\u{99999}" "\"你好\""}
⍤⤙≍ "\"n\\n\\\\n \\t \\\" ' \"" repr "n\n\\n \t \" ' "
⍤⤙≍ "{¯i ¯π}" repr {¯i ¯π}
⍤⤙≍ "[True False]" repr [True False]
⍤⤙≍ "$x 5" repr $x 5

# Sort
⍤⤙≍ [1 2 4 4 8] ⍆ [4 8 2 1 4]
⍤⤙≍ [2_1_1 5_2_0 5_3_5] ⍆ [5_3_5 2_1_1 5_2_0]

# Experimental!

# Binary round-trip
⍤⤙≍ ⟜⍜binary∘ [1 2 3 4]
⍤⤙≍ ⟜⍜binary∘ {"Hello" "World!"}
⍤⤙≍ ⟜⍜binary∘ °△2_3_4_5
⍤⤙≍ ⟜⍜binary∘ {1 η_π_τ 4_5_6 "wow!"}
⍤⤙≍ ⟜⍜binary∘ map [1 2 3] [4 5 6]
⍤⤙≍ ⟜⍜binary∘ ⇡256
⍤⤙≍ ⟜⍜binary∘ ⇡257
⍤⤙≍ ⟜⍜binary∘ ÷⟜⇡256
⍤⤙≍ ⟜⍜binary∘ ×π ⇡256

# FFT
⍤⤙≍ ⟜⍜fft∘ ℂ0[1 2 3 4]
⍤⤙≍ ⟜⍜°fft∘ ℂ0[1 2 3 4]
⍤⤙≍ ⟜(⁅₁₀⍜fft∘) ℂ0[1 2 3]
⍤⤙≍ ⟜(⁅₁₀⍜°fft∘) ℂ0[1 2 3 5]
⍤⤙≍ ⟜(⁅₁₀⍜fft∘) ℂ0°△3_3
⍤⤙≍ ⟜(⁅₁₀⍜°fft∘) ℂ0[1 2 3]
⍤⤙≍ ⟜(⁅₁₀⍜fft∘) ℂ0[1 2 3 5]
⍤⤙≍ ⟜(⁅₁₀⍜°fft∘) ℂ0°△3_3
