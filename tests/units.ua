# Comptime (These come first so that they are not the source of later errors)
X ← 5
Y ← comptime(X)
⍤⤙≍ X Y
Z ← comptime(+1Y)
⍤⤙≍ Z +1 X

# Basic pervasive math
⍤⤙≍ [2 3 4] +1 [1 2 3]
⍤⤙≍ [5 7 9] +[1 2 3][4 5 6]
⍤⤙≍ [5 6 7] +[1 2 3] 4
⍤⤙≍ [2_3_4 6_7_8 10_11_12] + [1 2 3] [1_2_3 4_5_6 7_8_9]
⍤⤙≍ [2_3_4 6_7_8 10_11_12] + [1_2_3 4_5_6 7_8_9] [1 2 3]
⍤⤙≍ [0_1_2 2_3_4 4_5_6] - [1 2 3] [1_2_3 4_5_6 7_8_9]
⍤⤙≍ ¯[0_1_2 2_3_4 4_5_6] - [1_2_3 4_5_6 7_8_9] [1 2 3]
⍤⤙≍ ⟜(×1_2) ↯2_0 0
⍤⤙≍ ⟜(×2) ↯0_2 0
⍤⤙≍ ⟜(×2) ↯0_0 0
⍤⤙≍ ⟜(×2) ↯0_0_0 0
⍤⤙≍ ⟜(×[2]) ↯0_0_0 0
⍤⤙≍ ↯2_0_2e ×2↯2_0_2e

# Fixed pervasive math
⍤⤙≍ ⊃≡++ ¤100_200 [[1_2 3_4] [5_6 7_8] [9_10 11_12]]
⍤⤙≍ ⊃≡≡++ ¤¤100_200 [[1_2 3_4] [5_6 7_8] [9_10 11_12]]
⍤⤙≍ ⊃≡++ 10_20 [1_2_3 4_5_6]
⍤⤙≍ ⊃≡++ ¤10_20_30 [1_2_3 4_5_6]
⍤⤙≍ ⊃≡-- ¤[1 2 3] [4 5 6]
⍤⤙≍ 0_3 △ +¤ ↯3 0 ↯0_3 0
⍤⤙≍ 1_3 △ +¤ ↯3 0 ↯1_3 0
⍤⤙≍ ⊃≡≡++ ¤¤0 ⇡10
⍤⤙≍ ⊃≡++ [0_1_2 3_4_5] ¤ [0_3_6 1_4_7 2_5_8]
⍤⤙≍ ⊃≡++ ¤ [0_1_2 3_4_5 6_7_8] [0_3_6 1_4_7 2_5_8]
⍤⤙≍ [11_12_13 21_22_23 31_32_33 41_42_43] + ¤[1 2 3] [10 20 30 40]
⍤⤙≍ [11_21_31_41 12_22_32_42 13_23_33_43] + ⊙¤[1 2 3] [10 20 30 40]
⍤⤙≍ [11_22_33 41_52_63] + ¤[1 2 3] [10_20_30 40_50_60]
⍤⤙≍ ⊃≡++ ¤ [10 20 30] ↯4_3_2⇡24
⍤⤙≍ ⊃≡++ ¤¤ [10 20] ↯4_3_2⇡24
⍤⤙≍ ≡≡(⊟.) [0_2 2_4] - °△ 2_1_2 °△ 2_2_2
⍤⤙≍ ≡≡(⊟.) ¯[0_2 2_4] - °△ 2_2_2 °△ 2_1_2
⍤⤙≍ [0_1 1_2] +∩°△ 2_1 1_2
⍤⤙≍ [[0_2 2_4 4_6] [2_4 4_6 6_8]] +∩°△ 2_1_2 1_3_2

# Filled pervasives
⍤⤙≍ [1_1 0_0] ⬚0↥ ↯ 2_2 0 ↯ 1_2 1
⍤⤙≍ [1_0 1_0] ⬚0↥ ↯ 2_2 0 ↯ 2_1 1
⍤⤙≍ [1 1] ⬚NaN= [1] [1 NaN]
⍤⤙≍ [3 3.5] ⬚0.5+ [1] [2 3]
⍤⤙≍ [.[.5]] ⬚5↥ [] [0_0 0_0]
⍤⤙≍ [.[.5]] ⬚5↥ [[][]] [0_0 0_0]
⍤⤙≍ [.[.5]] ⬚5↥ [0_0 0_0] []
⍤⤙≍ [.[.5]] ⬚5↥ [0_0 0_0] [[][]]
⍤⤙≍ [..0] ⬚@\0= "" "abc"
⍤⤙≍ [..0] ⬚@\0= "abc" ""
⍤⤙≍ [1 0 0] ⬚@\0= "abc" "a"
⍤⤙≍ [1 0 0] ⬚@\0= "a" "abc"
⍤⤙≍ @6 ⬚@0/+ [1 2 3]

# Trig
⍤⤙≍ η °∿ 1
⍤⤙≍ ℂ0η °∿ ℂ0 1
⍤⤙≍ η +η°∿ 0
⍤⤙≍ ℂ0η +η°∿ ℂ0 0
⍤⤙≍ η ∠ 1 0
⍤⤙≍ ℂ0η ∠ 1 ℂ0 0
⍤⤙≍ ℂ0η ∠ ℂ0 1 0
⍤⤙≍ ℂ0η ∠ ℂ0 1 ℂ0 0

# Bytes integrity - ensure the the boolean flag is properly set and unset
⍤⤙≍ ¯4 ¬5
⍤⤙≍ 1 ¬0
⍤⤙≍ 0 ¬1
⍤⤙≍ 0.5 ¬0.5
⍤⤙≍ [1 0 ¯1 ¯2] ¬[0 1 2 3]
⍤⤙≍ [1 0 0 1] ¬[0 1 1 0]
⍤⤙≍ [0 0 1] ¬∈ [1 2 3 4] [2 4 5]
⍤⤙≍ [1 0 1 0] ¬∈ [2 4] [1 2 3 4]
⍤⤙≍ [¯1 1 ¯1] ¬+. [1 0 1]
⍤⤙≍ [0 1 0] ¬⊂ 1 [0 1]
⍤⤙≍ [¯4 1 0] ¬⊂ 5 [0 1]
⍤⤙≍ [0 1] ¬⍜⊢(+1) [0 0]
⍤⤙≍ [1 0] ¬⍜(⊢⇌|+1) [0 0]
⍤⤙≍ [0 1] ¬⍜(⊡0|+1) [0 0]
⍤⤙≍ [1 0] ¬⍜(⊡1|+1) [0 0]
⍤⤙≍ [0 1] ¬⍜(⊏0|+1) [0 0]
⍤⤙≍ [1 0] ¬⍜(⊏1|+1) [0 0]
⍤⤙≍ [0 0] ¬⍜(⊏0_1|+1) [0 0]
⍤⤙≍ [0 1] ¬⍜(▽1_0|+1) [0 0]

# Dip
⍤⤙≍ [1 5] [⊙+ 1 2 3]
⍤⤙≍ [1 2 7] [⊙⊙+ 1 2 3 4]

# With
⍤⤙≍ [5 ¯3 5] [⤙¯ 3 5]
⍤⤙≍ [3 5 ¯3 5] [⟜⤙¯ 3 5]
⍤⤙≍ [5 8] [⤙+ 3 5]
⍤⤙≍ [4 1 2 3 4] [⤙⊙⊙⊙∘ 1 2 3 4]
⍤⤙≍ [4 1 2 3] [⤙⊙⊙⊙◌ 1 2 3 4]
⍤⤙≍ [3 1 2 3] [⤙1 2 3]

# Below
⍤⤙≍ [6 1 2 3] [◡(++)] 1 2 3
⍤⤙≍ [¯1 2 1 2] [◡¯] 1 2

# Fork
⍤⤙≍ [8 2] [⊃+- 3 5]
⍤⤙≍ [13 3 40 1.6] [⊃⊃⊃+-×÷ 5 8]
⍤⤙≍ [8 ¯3] [⊃+¯ 3 5]
⍤⤙≍ [¯3 8] [⊃¯+ 3 5]
⍤⤙≍ [8 2 15 4] [⊃(+|-|×|+1) 3 5]
⍤⤙≍ [8 2 15 4] [⊃⊃⊃+-×(+1) 3 5]
⍤⤙≍3 ⊃+() 1 2

# Empty array types
⍤⤙≍ 0 type[]
⍤⤙≍ 1 type""
⍤⤙≍ 2 type{}

# Two sum
⍤⤙≍ [0 1] ⊚/↥=⊞+. [2 7 11 15] 9

# Planet notation
⍤⤙≍ 25 ×⊃(+⊙⋅⋅∘)(-⊃⋅⋅∘(×⋅⊙⋅∘)) 1 2 3 4
⍤⤙≍ 25 ×⊃(+⊙⋅⋅∘)⋅(-⊃⋅∘(×⊙⋅∘)) 1 2 3 4

# Array construction
⍤⤙≍ [1] [∘] 1
⍤⤙≍ [[1]] [[∘]] 1
⍤⤙≍ [[3]] [[+]] 1 2
⍤⤙≍ [1 1] [.] 1
⍤⤙≍ [2 1] [:] 1 2
⍤⤙≍ [2 1 2] [,] 1 2
⍤⤙≍ [1 2 2] [⊙.] 1 2
⍤⤙≍ [¯1 ¯2] [∩¯] 1 2
⍤⤙≍ [1_0_0 2_3_0 4_5_6] ⬚0[1 2_3 4_5_6]

# Switch
⍤⤙≍ [¯1 2 ¯3 4 ¯5] ⨬(¯|∘) =0◿2.[1 2 3 4 5]
⍤⤙≍ [6 2 8 4 10] ⨬(⋅∘|∘) [0 1 0 1 0] [1 2 3 4 5] [6 7 8 9 10]
⍤⤙≍ [5 4 ¯1 8 ¯4] ⨬(-|+) =,, [1 2 3 4 5] [6 2 2 4 1]
⍤⤙≍ [¯1 2 ¯3 4 ¯5] ⨬(¯|∘) =0◿2.[1 2 3 4 5]
⍤⤙≍ [6 2 8 4 10] ⨬(⋅∘|∘) [0 1 0 1 0] [1 2 3 4 5] [6 7 8 9 10]
⍤⤙≍ [5 4 ¯1 8 ¯4] ⨬(-|+) =,, [1 2 3 4 5] [6 2 2 4 1]
⍤⤙≍ [81 1 57 1] ⨬(⊙◌|1) [0 1 0 1] [81 ¯1 57 ¯1] [79 14 55 13]
⍤⤙≍ [81 14 57 13] ⨬(⊙◌|◌) [0 1 0 1] [81 ¯1 57 ¯1] [79 14 55 13]
⍤⤙≍ 5 ⨬(5|0 ⍤.0) 0
F ← ⨬(¯|+1|×10|∞)
⍤⤙≍ [¯5 6 50 ∞] F [0 1 2 3] 5
⍤⤙≍ [¯1_2_10_∞ ¯2_3_20_∞ ¯3_4_30_∞] F [..0_1_2_3] [1 2 3]
⍤⤙≍ [..¯5_6_50_∞] F [..0_1_2_3] 5
⍤⤙≍ ↯0_3e ⨬∘∘[] ¤[1 2 3]
⍤⤙≍ ↯0_0e ⨬∘∘[] ¤[]

# Escape sequences
⍤⤙≍ 0 -@\0 @\0
⍤⤙≍ 9 -@\0 @\t
⍤⤙≍ 10 -@\0 @\n
⍤⤙≍ 13 -@\0 @\r
⍤⤙≍ 34 -@\0 @"
⍤⤙≍ 39 -@\0 @\'
⍤⤙≍ 65 -@\0 @A
⍤⤙≍ 92 -@\0 @\\
⍤⤙≍ 97 -@\0 @a
⍤⤙≍ 27 -@\0 @\x1b
⍤⤙≍ 4096 -@\0 @\u1000
⍤⤙≍ 1 -@\0 @\u{1}
⍤⤙≍ 16 -@\0 @\u{10}
⍤⤙≍ 256 -@\0 @\u{100}
⍤⤙≍ 4096 -@\0 @\u{1000}
⍤⤙≍ 65536 -@\0 @\u{10000}
⍤⤙≍ 1048576 -@\0 @\u{100000}

# Fill boundaries
⍤⤙≍ [0 1 2 3] ⬚0/⊂ [1 2 3]
⍤⤙≍ [0 1 2 3] ⬚0(/⊂) [1 2 3]
⍤⤙≍ [0 1 2 3] ⬚0((/⊂)) [1 2 3]

F ← /⊂
⍤⤙≍ [1 2 3] ⬚0F [1 2 3]
F ← ⬚°◌/⊂
⍤⤙≍ [0 1 2 3] ⬚0F [1 2 3]
M! ← /^0
⍤⤙≍ [1 2 3] ⬚0M!⊂ [1 2 3]
M! ← ⬚°◌/^0
⍤⤙≍ [0 1 2 3] ⬚0M!⊂ [1 2 3]
M! ←^ $"/_"⊢
⍤⤙≍ [0 1 2 3] ⬚0M!⊂ [1 2 3]
M! ←^ $"⬚°◌/_"⊢
⍤⤙≍ [0 1 2 3] ⬚0M!⊂ [1 2 3]

# Proxy values
F ← +@A ⊟.
⍤⤙≍ ⊂:2_1 ⟜(⊂⊃△type≡F⇡)2
⍤⤙≍ ⊂:2_1 ⟜(⊂⊃△type≡F⇡)1
⍤⤙≍ ⊂:2_1 ⟜(⊂⊃△type≡F⇡)0
⍤⤙≍ [0 3] △ ≡⍜▽∘ ↯0_3 1 ¤"abc"
⍤⤙≍ [0 3] △≡⍜(°⊟▽): ↯0_3[1_1_0] ¤"abc"

# Filled inverses
⍤⤙≍ 0 ⬚0°◌
⍤⤙≍ [0 1] ⬚[0 1]°◌
⍤⤙≍ [[1_2 3_0] [1_2 4_0]] ⬚0≡(⬚°◌⊟1_2) [3 4]

# Un chains
⍤⤙≍ [2 2] °/× 4
⍤⤙≍ 4 °°/× 4
⍤⤙≍ [2 2] °°°/× 4
⍤⤙≍ 4 °°°°/× 4
⍤⤙≍ [2 2] °°°°°/× 4
⍤⤙≍ 4 °°°°°°/× 4
⍤⤙≍ 3 °°+ 1 2
⍤⤙≍ 3 °°°°°°+ 1 2

# Anti anti
⍤⤙≍ 7 ⌝⌝+ 2 5
⍤⤙≍ 7 ⌝(⌝+∘) 2 5
⍤⤙≍ 7 ⌝(∘⌝+) 2 5

# Primes
⍤⤙≍ ↯0_1e °/× [1]
⍤⤙≍ ¤¤2 °/× [2]

# On-inverses
⍤⤙≍ [3 2] [°⟜+ 3 5]
⍤⤙≍ [3 2] [°⟜× 3 6]
⍤⤙≍ [3 8] [°⟜- 3 5]
⍤⤙≍ [3 18] [°⟜÷ 3 6]
⍤⤙≍ [3 2] [°⟜(+?) 3 5]
⍤⤙≍: [3 5] [°⟜↥ ⟜↥ 3 5]
⍤⤙≍: [5 3] [°⟜↧ ⟜↧ 5 3]
⍤⤙≍ [1_2 3_4] [°⟜⊂ 1_2 1_2_3_4]

# By-inverses
Test! ← ⍤⤙≍ ⊃⊟(⍜×⁅1e12[⊸^0°⊸^0]) 2 5
Test!+
Test!-
Test!(-:)
Test!×
Test!÷
Test!(÷:)
Test!ⁿ
Test!(ⁿ:)
Test!ₙ
Test!(ₙ:)

# Select inverses
⍤⤙≍ [0 1 1 2] ⊙◌°⊸⊏ [1 2 2 3] [1 2 3 2]
⍤⤙≍ [0 0 0] ⊙◌°⊸⊏ [..1_2] [1_2 3_4 1_2]
⍤⤙≍ 0 ⊙◌°⊸⊏ 5 5
⍤⤙≍ [......15 7] ⊙◌°⊸⊏ "fffffff7" "0123456789abcdef"

⍤⤙≍ "c-b-a" ◌⬚@-°⟜⊏ [4 2 0] "abc"
⍤⤙≍ "b-c-a" ◌⬚@-°⟜⊏ [4 0 2] "abc"
⍤⤙≍ "-abcde----f" ◌⬚@-°⟜⊏ [1_2 3_4 5_10] ["ab" "cd" "ef"]
⍤⤙≍ ["--""ab""cd""ef"] ◌⬚@-°⟜⊏ [1 2 3] ["ab" "cd" "ef"]
⍤⤙≍ [1] ◌°⟜⊏ 0_0 1_1

# Pick inverses
⍤⤙≍ [0 0 5] ◌⬚0°⟜⊡ 2 5
⍤⤙≍ [0_0_0_0 0_0_0_6 0_0_5_0] ◌⬚0°⟜⊡ [2_2 1_3] 5_6
⍤⤙≍ [.[...0_0][0_0 0_0 0_0 5_6]] ◌⬚0°⟜⊡ 2_3 5_6
⍤⤙≍ [0_0_0 0_0_5] ◌⬚0°⟜⊡ 1_2 5
⍤⤙≍ [4_1 2_3] ◌°⟜⊡ [0_1 1_0 1_1 0_0] 1_2_3_4

# General on inverse
F ← ×2+1
⍤⤙≍ [1 4] [°⟜(F+) ⟜(F+) 1 4]
⍤⤙≍ [1 4] [°⟜(+F) ? ⟜(+F) 1 4]
⍤⤙≍ [1 4] [°⟜(F+F) ⟜(F+F) 1 4]

# Various bindings
X ← |0 5
⍤⤙≍ 5 X
6

X ← |0
⍤⤙≍ 6 X
X ← |0.3 1 2 3
⍤⤙≍ [1 2 3] [X]
1 2 3

X ← |0
⍤⤙≍ [1] [X]
◌◌
⍤⤙≍ [1 10] X_10

G ↚ +1
F ← G

F ←
≡F

# Constant vs Noadic function
F ← ⚂
G ← (⚂)

⍤⤙≍ 1 ⧻◴ [F F F]
⍤⤙≍ 3 ⧻◴ [G G G]

# Euler's identity
⍤⤙≍ 0 ⍜(×1e12)⁅ ⌵+1 ⁿ×iπ e

# Format strings
⍤⤙≍ "5" $"_" 5
⍤⤙≍ "1, 2, 3" $"_, _, _" 1 2 3
⍤⤙≍ "1 + 2 = 3" $"_ + _ = _" ⊃⊙∘+ 1 2
⍤⤙≍ ["foo5" "bar5"] ⊟⊃$"foo_"$"bar_" 5
$ Multi _
$ _ line
⍤⤙≍ "Multi _\n_ line"
"foo" "bar"
$$ Multi _
$$ _ line
⍤⤙≍ "Multi foo\nbar line"

[1 ¯5 +0.1 0.2 π ∞ NaN]
⍤⤙≍ ⟜≡(⋕°⋕).
⍤⤙≍ ⟜≡(⋕$"_")

# Try
{"cat" "sand"} "dog" {"cat" "cats" "and" "sand" "dog"}
⍤⤙≍ {{"cat" "sand"} {"dog"} {""}} {⊙(∩▽⟜:≡°□⍚⍣(1°.⊙⊃↙↘⟜⧻|0⊙∘):¤)}
⍤⤙≍ [2 2] [⍣√⋅. 4 2]
⍤⤙≍ [3] [⍣(⋅2⍤.0)3] 1

F ← ⍣⨬(⍤.$"_"|¯)⋅⋅∘
⍤⤙≍ "5" F 0 5
⍤⤙≍ ¯5 F 1 5

# Pattern matching
⍤⤙≍ 2 °[1⊙3] [1 2 3]
⍤⤙≍ [1 2 4 5] [°[⊙⊙(3⊙∘)] [1 2 3 4 5]]
⍤⤙≍ 2 °(⊂0[1⊙3]) [0 1 2 3]
⍤⤙≍ {"1" "2"} {°$"_ - _"} "1 - 2"
⍤⤙≍ {"1" " " "2"} {°$"_ -__"} "1 - 2"
⍤⤙≍ {"5"} {°$"_"} "5"
⍤⤙≍ {} {°$"wow"} "wow"
⍤⤙≍ {"1" "2\n5"} {°$"_-_" "1-2\n5"}
⍤⤙≍ 1 °. 1 1
⍤⤙≍ 5 °¤ [5]
⍤⤙≍ [5] [°⟜(+3) 5 8]
⍤⤙≍ [1 2 3] °(↥1) [1 2 3]
⍤⤙≍ 0 ⍣°(↧ 1)0 [1 2 3]
⍤⤙≍ [1 2 3] °(↥ℂ0 0) [1 2 3]
⍤⤙≍ [1 2 3] °(↧ℂ4 4) [1 2 3]
⍤⤙≍ 2 °(⊟.) [2 2]
°(+1 2) 3
F ← {⊓$A$B} # Track caller!
°(F 1 2) F 1 2

# Wildcard
⍤⤙≍ [W 2 3] [1 2 3]
⍤⤙≍ [1 W 3] [1 2 3]
⍤⤙≍ "hello" "he\Wlo"

# Complex
⍤⤙≍ i ℂ1 0
⍤⤙≍ ℂ0 4 ◿ 5 ℂ0 ¯1
⍤⤙≍ ×2i √ ℂ0¯4
⍤⤙≍ ×2i ⍜×⁅1e15 ⁿ1/2 ℂ0¯4

# Memo
F ← memo(+⌊×10⚂)
⍤⤙≍ F1 F1
⍤⤙≍ F5 F5

# Recursion
Fact ← |1 ⨬(×Fact-1.|1)<2.
⍤⤙≍ 120 Fact 5
A ← |1 ⨬(+⊃(A-1|A-2)|1)<2.
⍤⤙≍ 21 A 7
B ← |1 (⨬(+⊃(B-1|B-2)|1)<2.)
⍤⤙≍ 21 B 7

# On
⍤⤙≍ [1 1 3] [⟜⊙⋅⊙◌ 1 2 3 4]
⍤⤙≍ [1 1 2 2 4] [⟜⊙⟜⊙⋅∘ 1 2 3 4]
⍤⤙≍ [1 3] [⟜3 1]

# By
⍤⤙≍ [0 5] [⊸0] 5
⍤⤙≍ [8 5] [⊸+] 3 5
⍤⤙≍ [6 3] [⊸(++)] 1 2 3

# Anti call
F ← ⌅(+|-) # Track Caller!
⍤⤙≍ 7 F 2 5
⍤⤙≍ 3 ⌝F 2 5

# Composition with contra inverse
F ← ⌅⍜⊢¯ ⊏⊙"nubtqphsoe" -@0 °⋕
⍤⤙≍ ⟜(°F F) 118

# ThisFile
⍤⤙≍ $"tests_units.ua" Sep ThisFile
⍤⤙≍ ThisFile /◇⊂ {ThisFileDir Sep ThisFileName}
◌&fras /◇⊂ {WorkingDir Sep ThisFile}
◌&fras /◇⊂ {WorkingDir Sep ThisFileDir Sep ThisFileName}

# Pointer equality
⍤⤙≍ 1 ≍ NULL NULL
⍤⤙≍ 0 ≍ NULL []
⍤⤙≍ 0 ≍ [] NULL

# Experimental!

# Off
⍤⤙≍ [¯3 5 3] [⤚¯ 3 5]
⍤⤙≍ [¯3 5 3 5] [⊸⤚¯ 3 5]
⍤⤙≍ [8 3] [⤚+ 3 5]
⍤⤙≍ [1 2 3 4 1] [⤚⊙⊙⊙∘ 1 2 3 4]
⍤⤙≍ [2 3 4 1] [⤚⋅⊙⊙∘ 1 2 3 4]
⍤⤙≍ [1 2 3 2] [⤚1 2 3]

# Above
⍤⤙≍ [1 2 3 6] [◠(++)] 1 2 3
⍤⤙≍ [1 2 ¯1 2] [◠¯] 1 2

# Subscript both
⍤⤙≍ [] [∩₀+]
⍤⤙≍ [3] [∩₁+ 1 2]
⍤⤙≍ [3 7] [∩₂+ 1 2 3 4]
⍤⤙≍ [3 7 11] [∩₃+ 1 2 3 4 5 6]
⍤⤙≍ [3 7 11 15] [∩₄+ 1 2 3 4 5 6 7 8]
