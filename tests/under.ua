⍤⟜≍: [10 20 3 4 5] ⍜(↙2|×10) +1⇡5
⍤⟜≍: [1 2 30 40 50] ⍜(↘2|×10) +1⇡5
⍤⟜≍: [1 2 3 4 5] ⍜(↘5|×10) +1⇡5
⍤⟜≍: [1 2 3 4 5] ⍜(↘8|×10) +1⇡5
⍤⟜≍: [1 2 30 40 5] ⍜(↙2↘2|×10) +1⇡5
⍤⟜≍: [1 20 3 4 50] ⍜(⊏1_4|×10) +1⇡5
⍤⟜≍: [0_1_2 30_40_50 6_7_8] ⍜(⊏1|×10) ↯3_3⇡9
⍤⟜≍: [0_1_2 30_40_50 6_7_8] ⍜(⊏[1]|×10) ↯3_3⇡9
⍤⟜≍: [0_1_2 30_40_50 60_70_80] ⍜(⊏1_2|×10) ↯3_3⇡9
⍤⟜≍: ¤¤¤¤¤¤20 ⍜(⊏0|×10) ¤¤¤¤¤¤2
⍤⟜≍: [1 2 30 4 5] ⍜(⊡2|×10) +1⇡5
⍤⟜≍: [0_1_2 3_40_5 6_7_8] ⍜(⊡1_1|×10) ↯3_3⇡9
⍤⟜≍: [0_1_20 30_4_5 6_7_8] ⍜⊡(×10) [1_0 0_2] ↯3_3⇡9
⍤⟜≍: [1_5_3 3_4_2] [⍜∩⊡: 1 1_2_3 2 3_4_5]
⍤⟜≍: [2_1 3_4] [⍜∩°⊟: 1_2 3_4]
⍤⟜≍: [6 20] ⍜∩°⊟∩× 2_3 4_5

⍤⟜≍: [6_5 4_3 2_1] ⍜♭⇌ [1_2 3_4 5_6]
⍤⟜≍: [1_2_3] ⍜△(⊂1) [1 2 3]

# Both
⍤⟜≍: [1 2 3 4] [⍜∩⊟∘ 1 2 3 4]
⍤⟜≍: [3 4 1 2] [⍜∩⊟: 1 2 3 4]
⍤⟜≍: [3 4 1 2] [⍜∩{⊙∘}: 1 2 3 4]
⍤⟜≍: [3 4 1 2] [⍜∩({⊙∘}): 1 2 3 4]
⍤⟜≍: [1 2 3 4] [⍜∩{⊙∘}∘ 1 2 3 4]
⍤⟜≍: [1 2 3 4] [⍜∩({⊙∘})∘ 1 2 3 4]
⍤⟜≍: 5 ⍜∩(×.)+ 3 4
⍤⟜≍: {6 5} [⍜∩°□:□5 □6]
⍤⟜≍: [9 9 5] ⍜⊡(⍜∩×↥,) 2 [⍥9]3 ¯1 5
⍤⟜≍: [3 2] ⍜∩▽◌⟜: [1 0] [1 2] [3 4]
⍤⟜≍: 1 ⍣(0◌)1 ⍜(∩(|1 ◌ °&ime &frab))∩∘ ."site/favicon.ico"
⍤⟜≍: [.4] [⍜∩(◌setinv(⊓⌊⌈.÷2|+)): 3 5]

# On
⍤⟜≍: [3 5] [⍜⟜×∘ 3 5]
⍤⟜≍: [3 5] [⍜⟜÷∘ 3 5]
⍤⟜≍: [3 5] [⍜⟜(÷:)∘ 3 5]

# Normal Outside
⍤⟜≍: 5 ⍜+∘ 3 5
⍤⟜≍: 5 ⍜-∘ 3 5
⍤⟜≍: 5 ⍜×∘ 3 5
⍤⟜≍: 5 ⍜÷∘ 3 5
⍤⟜≍: 5 ⍜(×1e12)⁅ ⍜ⁿ∘ 3 5
⍤⟜≍: 5 ⍜(×1e12)⁅ ⍜ₙ∘ 3 5
# Flipped Outside
⍤⟜≍: 5 ⍜(+:)∘ 3 5
⍤⟜≍: 5 ⍜(-:)∘ 3 5
⍤⟜≍: 5 ⍜(×:)∘ 3 5
⍤⟜≍: 5 ⍜(÷:)∘ 3 5
⍤⟜≍: 5 ⍜(×1e12)⁅ ⍜(ⁿ:)∘ 3 5
⍤⟜≍: 5 ⍜(×1e12)⁅ ⍜(ₙ:)∘ 3 5
# Normal Inside
⍤⟜≍: 5 ⍜(+3)∘ 5
⍤⟜≍: 5 ⍜(-3)∘ 5
⍤⟜≍: 5 ⍜(×3)∘ 5
⍤⟜≍: 5 ⍜(÷3)∘ 5
⍤⟜≍: 5 ⍜(×1e12)⁅ ⍜(ⁿ3)∘ 5
⍤⟜≍: 5 ⍜(×1e12)⁅ ⍜(ₙ3)∘ 5
# Flipped Inside
⍤⟜≍: 5 ⍜(+:3)∘ 5
⍤⟜≍: 5 ⍜(-:3)∘ 5
⍤⟜≍: 5 ⍜(×:3)∘ 5
⍤⟜≍: 5 ⍜(÷:3)∘ 5
⍤⟜≍: 5 ⍜(×1e12)⁅ ⍜(ⁿ:3)∘ 5
⍤⟜≍: 5 ⍜(×1e12)⁅ ⍜(ₙ:3)∘ 5

⍤⟜≍: 5 ⍜°√(+9) 4
⍤⟜≍: 5 ⍜∩°√+ 3 4

⍤⟜≍: 5 ⍜±∘ 5
⍤⟜≍: ¯5 ⍜±∘ ¯5
⍤⟜≍: 10 ⍜±(+1) 5
⍤⟜≍: ¯10 ⍜±(-1) ¯5

⍤⟜≍: 1.5 ⍜⌊∘ 1.5
⍤⟜≍: 1.5 ⍜⌈∘ 1.5
⍤⟜≍: 1.5 ⍜⁅∘ 1.5

# Fill
⍤⟜≍: [0 0 3 4 5 6] ⍜⬚0↻∘ 2 [1 2 3 4 5 6]
⍤⟜≍: [0 0 3 4 5 6] ⍜(⬚0↻2)∘ [1 2 3 4 5 6]

# Join
⍤⟜≍: {4 [3 2 1]} {⍜⊂⇌ 1 [2 3 4]}
⍤⟜≍: {5_6 [3_4 1_2]} {⍜⊂⇌ 1_2 [3_4 5_6]}
⍤⟜≍: {1 [2 3 4]} {°⊂ [1 2 3 4]}
⍤⟜≍: {0_1_2_3 [4_5_6_7 8_9_10_11]} {°⊂ ↯3_4⇡12}
⍤⟜≍: {¯[1 2] ¯[3 4 5]} {⍜⊂¯ [1 2] [3 4 5]}

⍤⟜≍: [.↯5 0 . 0_0_1_1_0 ↯5 0] ⍜(↙2_2↘2_2)¬ ↯5_5 0
⍤⟜≍: [.↯5 0 . 0_0_1_1_0 ↯5 0] ⍜(↙2_2↙¯3_¯3)¬ ↯5_5 0

⍤⟜≍: [1 2 3 4] ⍜(↻1)∘ [1 2 3 4]
⍤⟜≍: [1 2 3 4] ⍜↻∘ 1 [1 2 3 4]

⍤⟜≍: "World! Hello" ⍜⊜□⇌ ≠@ . "Hello World!"
⍤⟜≍: "Just A Little Guy" ⍜⊜⊢⌵ ≠@ . "Just a little guy"
⍤⟜≍: {"Hello" "World!"} {⍜∩⊜□∘ ∩(≠@l.) "Hello" "World!"}
⍤⟜≍: "sdrow emo sera esehT" ⍜⊕□≡⍜°□⇌ ≠@ . $ These are some words

⍤⟜≍: ⍜⍉∘. ↯3_3⇡9
⍤⟜≍: ⍜⍉∘. ↯2_3_4_5⇡120

⍤⟜≍: [1 20 30 4 5] ⍜(↙⊙↘|×10) 2 1 [1 2 3 4 5]
⍤⟜≍: [10 20 3 4 5] ⍜(↙⊙↙|×10) 2 4 [1 2 3 4 5]
⍤⟜≍: [1 2 30 40 5] ⍜(↙:↘|×10) 2 [1 2 3 4 5] 2
⍤⟜≍: [10_20_30 4_5_6] [⍜⊙⇌(×10) [1 2 3] [4 5 6]]

⍜⊙(☇1)≡(⊂⊂) ⊓(☇0|∘|☇0) ⊃(∘|↯2|∘)↯2_2⇡4
⍤⟜≍: [[0_0_1_0 1_2_3_1] [2_0_1_2 3_2_3_3]]
⍜⊙⊙(☇1)≡(⊂⊂) ∩(☇0) ⊃.(↯2)↯2_2⇡4
⍤⟜≍: [[0_0_0_1 1_1_2_3] [2_2_0_1 3_3_2_3]]

⍤⟜≍: ⟜⍜°(⍉⊟)∘ [1_2 3_4]

⍤⟜≍: ⊟.[1 2 3] ⍜(☇0)(⊟.) [1 2 3]

⍤⟜≍: □[1 2 3 4 5 6 7] ≡⍜∩°□(⊂⊂) □[1 2 3] □[4 5 6] 7

⍤⟜≍: [1 2 3] [°[⊙⊙∘] [1 2 3]]
⍤⟜≍: [3 5 6] ⍜°[⊙⊙∘]+ [1 2 5] 6
⍤⟜≍: [3 5 6] ⍜(°[⊙⊙∘])+ 1_2_5 6
⍤⟜≍: [6 1 7] ⍜(°[⊙⊙∘]⇌)+ 1_2_5 6
⍤⟜≍: [8 6 7] ⍜°[⊙⊙∘](++) [1 2 5] 6 7
⍤⟜≍: [8 6 7] ⍜(°[⊙⊙∘])(++) [1 2 5] 6 7
⍤⟜≍: {1 2 3} [°[⊙⊙∘] {1 2 3}]
⍤⟜≍: [1 2 3] [°{⊙⊙∘} {1 2 3}]
⍤⟜≍: [11 12 13] [⍜[⊙⊙∘]+ 1 2 3 10]
⍤⟜≍: ⊃(⍜°⊟+|⍜°[⊙∘]+) [1 2] 4
⍤⟜≍: [¯1 ¯2 ¯3] [⍜[⊙⊙∘]¯ 1 2 3]
⍤⟜≍: [¯1 ¯2 ¯3 ¯4] [⍜[⊙⊙⊙∘]¯ 1 2 3 4]
⍤⟜≍: [1 2 3] ⍜[+1]∘ [1 2 3]
⍤⟜≍: [[2 3 4]] °°[+1] [1 2 3]

⍤⟜≍: ¯18 ⍜(⍥(+1)4)¯ 10
⍤⟜≍: ¯18 ⍜⍥(+1)¯ 4 10

⍤⟜≍: @l ⍜⊛(⊢⍖°⊚) "Hello World!"

F ← setund(setinv"normal""inverse"|"do"|"undo")
⍤⟜≍: "normal" F
⍤⟜≍: "inverse" °F
⍤⟜≍: {"undo""do"} {⍜F∘}
G ← setinvsetund("normal"|"do"|"undo")"inverse"
⍤⟜≍: "normal" G
⍤⟜≍: "inverse" °G
⍤⟜≍: {"undo""do"} {⍜G∘}

⍤⟜≍: : ⍜°/×∘ . 70
⍤⟜≍: : ⍜°/×∘ . +1⇡100
⍤⟜≍: : ⍜°/×∘ . +1↯2_3_4⇡24

F ← ⍜⊙⊙⊢ ⋅⊙∘
G ← ⍜(⊙⊙⊢) ⋅⊙∘
⍤⟜≍: {F 1 2 [3]} {G 1 2 [3]}

⍤⟜≍: [10 2 3] ⍜(〈⊢|⊢⇌〉|×10) 0 [1 2 3]
⍤⟜≍: [1 2 30] ⍜(〈⊢|⊢⇌〉|×10) 1 [1 2 3]
⍤⟜≍: [1 20 3] ⍜〈⊡|⊏〉(×10) 0 1 [1 2 3]
⍤⟜≍: [1 20 3] ⍜〈⊡|⊏〉(×10) 1 1 [1 2 3]
